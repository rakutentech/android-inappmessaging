// Reduce code duplication.
apply from: "$CONFIG.configDir/android/application.gradle"
apply from: "../config/quality/gitleaks/build.gradle"
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion CONFIG.versions.android.sdk.compile

    defaultConfig {
        applicationId "rakuten.com.tech.mobile.test"
        versionCode 1
        versionName "1.0.0"
        // Defines the minimum API level required to run the app.
        minSdkVersion CONFIG.versions.android.sdk.min
        // Specifies the API level used to test the app.
        targetSdkVersion CONFIG.versions.android.sdk.target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
        
        def property = { key ->
            return System.getenv(key) ?: (project.hasProperty(key) ? project."$key" : null)
        }

        manifestPlaceholders.in_app_messaging_sub_id = property("IAM_SUBSCRIPTION_KEY") ?: ""
        manifestPlaceholders.config_url = property("CONFIG_URL") ?: ""
//        manifestPlaceholders.event_logger_api_url = property("RELOGGER_API_URL") ?: ""
//        manifestPlaceholders.event_logger_api_key = property("RELOGGER_API_KEY") ?: ""

        buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        buildConfigField("String", "VERSION_CODE", "\"${defaultConfig.versionCode}\"")
        buildConfigField("String", "RELOGGER_API_URL", "\"${property("RELOGGER_API_URL")}\"")
        buildConfigField("String", "RELOGGER_API_KEY", "\"${property("RELOGGER_API_KEY")}\"")
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG-${new Date().format("yyyy-MM-dd")}"

            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}${versionNameSuffix}\"")
        }

        release {
            debuggable true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    flavorDimensions 'version'
}

dependencies {
    implementation project(':inappmessaging')
    implementation "io.github.rakutentech.sdkutils:sdk-utils:1.1.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$CONFIG.versions.kotlin"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.core:core-ktx:1.3.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0'
}
repositories {
    mavenCentral()
}
