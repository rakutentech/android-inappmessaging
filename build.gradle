buildscript {
    ext.dokka_version = '1.6.10'
    ext.compose_version = '1.1.1'

    // Build Config.
    apply from: 'config/index.gradle'

    CONFIG.versions.android.sdk.min = 23
    CONFIG.versions.android.sdk.target = 31
    CONFIG.versions.android.sdk.compile = 31
    CONFIG.versions.kotlin = '1.6.10'

    repositories {
        google()
        gradle()
        mavenCentral()
        jitpack()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$CONFIG.versions.android.plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$CONFIG.versions.kotlin"
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath 'org.owasp:dependency-check-gradle:6.5.3'
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version '1.19.0'
    id 'org.sonarqube' version '3.3'
}

allprojects { proj ->
    repositories {
        google()
        mavenCentral()
        jitpack()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
        }
    }
    apply plugin: 'pl.allegro.tech.build.axion-release'
    scmVersion {
        useHighestVersion = true
        tag {
            prefix = 'v'
            versionSeparator = ''
            versionIncrementer 'incrementMinor'
        }
    }

    proj.version = scmVersion.version

    apply plugin: 'com.github.ben-manes.versions'
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 100
    reports.html.enabled = false
    reports.junitXml.enabled = false
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
}

apply from: "$project.rootDir/dependency-check.gradle"
apply from: "$project.rootDir/sonar.gradle"