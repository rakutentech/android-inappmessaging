buildscript {
    ext.kotlin_version = '1.3.61'
    ext.dokka_version = '0.10.0'
    // Build Config.
    apply from: 'config/index.gradle'

    CONFIG.versions.android.sdk.min = 21
    CONFIG.versions.kotlin = '1.3.61'
    CONFIG.versions.android.plugin = '3.5.3'

    repositories {
        google()
        gradle()
        jcenter()
        jitpack()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$CONFIG.versions.android.plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$CONFIG.versions.kotlin"
        classpath "digital.wup:android-maven-publish:3.6.2"
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.25.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'net.ltgt.errorprone' version '1.1.1' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.1.1'
}

allprojects { proj ->
    repositories {
        google()
        jcenter()
        jitpack()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
        }
    }
    apply plugin: 'pl.allegro.tech.build.axion-release'
    scmVersion {
        useHighestVersion = true
        tag {
            prefix = 'v'
            versionSeparator = ''
            versionIncrementer 'incrementMinor'
        }
    }

    proj.version = scmVersion.version

    apply plugin: 'com.github.ben-manes.versions'
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 100
    reports.html.enabled = false
    reports.junitXml.enabled = false
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
}