apply from: '../config/android/library.gradle'
apply from: "../config/quality/checkstyle/android.gradle"
apply from: "../config/quality/detekt/android.gradle"
apply from: "../config/quality/gitleaks/build.gradle"

apply plugin: "kotlin-android"

android {
  defaultConfig {
    resValue 'string', 'inappmessaging__version', project.version
    versionName version
    consumerProguardFiles 'proguard-rules.pro'

    buildConfigField("boolean", "IS_CACHE_HANDLING", "true")
    buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
    buildConfigField("int", "IAM_RAT_ACC", CONFIG.property("IAM_RAT_ACC"))
    buildConfigField("int", "IAM_RAT_AID", CONFIG.property("IAM_RAT_AID"))

    resValue("string", "config_url", CONFIG.property("CONFIG_URL_TEST_PROD") ?: "")
    resValue("string", "test_sub_key", CONFIG.property("IAM_TEST_SUBSCRIPTION_KEY") ?: "test_sub_key")
  }

  sourceSets.each {
    it.java.srcDirs += "src/$it.name/kotlin"
  }

  kotlinOptions {
    jvmTarget = JavaVersion.toVersion(CONFIG.versions.java).toString()
  }

  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }

  dexOptions {
    javaMaxHeapSize = 6g
    maxProcessCount = 8
    threadCount = 8
  }

  buildTypes {
    release {
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  // For Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$CONFIG.versions.kotlin"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
  implementation 'androidx.work:work-runtime:2.7.1'
  implementation("androidx.work:work-runtime-ktx:2.7.1")
  implementation 'com.google.android.material:material:1.2.1'
  implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
  implementation 'com.squareup.retrofit2:retrofit:2.8.1'
  implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
  implementation 'com.google.code.gson:gson:2.9.1'
  implementation "com.squareup.picasso:picasso:2.71828"
  implementation "io.github.rakutentech.sdkutils:sdk-utils:2.2.0"
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
  implementation 'androidx.appcompat:appcompat:1.1.0'

  // Doclava needs an annotation class from this package (used by Retrofit)
  compileOnly 'org.codehaus.mojo:animal-sniffer-annotations:1.18'

  compileOnly project(":analytics")

  testImplementation "org.amshove.kluent:kluent-android:1.65"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9'
  testImplementation 'androidx.test.ext:junit:1.1.3'
  testImplementation 'androidx.test:core:1.4.0'
  testImplementation 'androidx.test:runner:1.4.0'
  testImplementation 'androidx.work:work-testing:2.7.1'
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.mockito:mockito-core:4.5.1'
  testImplementation 'org.robolectric:robolectric:4.10.3'
  testImplementation "com.facebook.soloader:soloader:0.9.0"
  testImplementation "com.squareup.okhttp3:mockwebserver:3.14.7"

  detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${detekt_version}"
}

task checkUrlPrefixes {
  doLast {
    if (System.env.CI && System.env.CIRCLECI) {
      println "Allow empty URL prefixes for config APIs on CI & circleCI build jobs"
    } else if (!project.hasProperty('CONFIG_URL')) {
      throw new GradleException("Cannot Bulid Runtime without default config prexfixes. Configure 'CONFIG_URL' in your environment's global gradle.properties")
    }
  }
}

checkstyle { // we rely on automated code formatting instead
  ignoreFailures true
  toolVersion '10.0'
}

apply from: '../config/publish/android.gradle'
afterEvaluate {
  publishing {
    publications {
      inappmessaging(MavenPublication, androidArtifact())
    }
  }
}

def isSnapshot = project.version.contains('-')
if (isSnapshot) {
  project.ext.REPOSITORY_URL=CONFIG.property("REPOSITORY_URL_SNAPSHOT")
  project.ext.REPOSITORY_NAME=CONFIG.property("REPOSITORY_NAME_SNAPSHOT")
  apply from: '../config/publish/repository.gradle'
} else {
  apply from: '../config/publish/maven-central.gradle'
}

apply from: '../config/documentation/dokka/android.gradle'
dokkaGfm.configure {
  dokkaSourceSets {
    named("main") {
      sourceRoots.from(file("inappmessaging/src/main"))
    }
  }
}

detekt {
  allRules = true
  config = rootProject.files('detekt-config.yml')
}

check.dependsOn 'detektRelease'

apply from: "../config/quality/jacoco/android.gradle"
jacoco {
  toolVersion = "0.8.7"
}